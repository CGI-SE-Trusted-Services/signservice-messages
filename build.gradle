plugins {
    id("maven-publish")
    id("groovy")
    id("idea")
    id("project-report")
}

group "se.signatureservice.messages"
description "signservice-messages"

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

configurations {
    jaxws
}

dependencies {
    implementation "org.bouncycastle:bcprov-jdk18on:${bcVersion}"
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.2'
    implementation("org.apache.santuario:xmlsec:2.3.5") {
        exclude(module: "woodstox-core")
        exclude(module: "commons-codec")
    }
    testRuntimeOnly 'org.glassfish.jaxb:jaxb-runtime:4.0.5'
    testRuntimeOnly 'net.bytebuddy:byte-buddy:1.17.5'

    testImplementation 'org.objenesis:objenesis:3.4'
    testImplementation "commons-io:commons-io:2.19.0"
    testImplementation "org.apache.tomcat.embed:tomcat-embed-core:${tomcatEmbedVersion}"
    testImplementation "org.codehaus.groovy:groovy-xml:${groovyVersion}"
    testImplementation platform("org.spockframework:spock-bom:2.3-groovy-3.0")
    testImplementation "org.spockframework:spock-core"

    jaxws 'com.sun.xml.ws:jaxws-ri:4.0.3'
}

tasks.register('jaxbEidDssExtensions113', JavaExec) {
    mainClass = 'com.sun.tools.xjc.XJCFacade'
    classpath = configurations.jaxws
    args = ['-d', 'src/main/java', '-disableXmlSecurity', 'src/main/resources/eid-dss-extensions-1.1.3.xsd', '-b', 'src/main/resources/xjb/1.xjb']
}

tasks.register('jaxbOasisDssCore10', JavaExec) {
    mainClass = 'com.sun.tools.xjc.XJCFacade'
    classpath = configurations.jaxws
    args = ['-d', 'src/main/java', '-disableXmlSecurity', 'src/main/resources/cs-message-oasis-dss-core-schema-v1.0-os.xsd', '-b', 'src/main/resources/xjb/2.xjb']
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
}
