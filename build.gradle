group "org.certificateservices.messages"
description "certservice-messages"

apply plugin: "java"
apply plugin: "maven-publish"
apply plugin: "groovy"
apply plugin: "idea"
apply plugin: "project-report"

ext {
    bcJdk18onVersion = "1.76"
    groovyVersion = "3.0.21"
    tomcatEmbedVersion = "9.0.93"
    commonsIOVersion = "2.16.1"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

pluginManager.withPlugin('org.certificateservices.gradle.certservice-gradle-plugin') {
    certserviceSettings {
        useMavenCentral = true
        licenseName = "GNU Lesser General Public License v3.0 or later"
        licenseURL = "https://www.gnu.org/licenses/lgpl-3.0-standalone.html"
        useDeveloperPOM = true
        useScmPOM = true
        scmConnection = "https://github.com/CGI-SE-Trusted-Services/certservice-messages.git"
        scmDeveloperConnection = "https://github.com/CGI-SE-Trusted-Services/certservice-messages.git"
        scmUrl = "https://github.com/CGI-SE-Trusted-Services/certservice-messages"
        projectUrl = "https://github.com/CGI-SE-Trusted-Services/certservice-messages"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.bouncycastle:bcprov-jdk18on:${bcJdk18onVersion}"
    implementation("javax.xml.bind:jaxb-api:2.3.1")
    implementation("javax.activation:javax.activation-api:1.2.0")
    implementation("org.apache.santuario:xmlsec:2.3.4") {
        exclude(module: "woodstox-core")
        exclude(module: "commons-codec")
    }
    testRuntimeOnly 'com.sun.xml.bind:jaxb-core:2.3.0.1'
    testRuntimeOnly 'com.sun.xml.bind:jaxb-impl:2.3.9'
    testRuntimeOnly 'net.bytebuddy:byte-buddy:1.14.19'

    testImplementation 'org.objenesis:objenesis:3.4'
    testImplementation platform("org.spockframework:spock-bom:2.3-groovy-3.0")
    testImplementation "commons-io:commons-io:${commonsIOVersion}"
    testImplementation "org.spockframework:spock-core"
    testImplementation "org.apache.tomcat.embed:tomcat-embed-core:${tomcatEmbedVersion}"
    testImplementation "org.codehaus.groovy:groovy-xml:${groovyVersion}"
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
}
